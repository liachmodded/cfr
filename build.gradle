plugins {
    id 'java'
    id 'maven-publish'
    id "net.kyori.blossom" version "1.1.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.0.2'

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

group = 'net.fabricmc'
archivesBaseName = 'cfr'

sourceSets {
    main {
        java.srcDirs = ['src', 'src-templates']
    }
}

java {
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
         it.options.release = 8
    }
}

blossom {
    replaceTokenIn 'src-templates/org/benf/cfr/reader/util/CfrVersionInfo.java'
    replaceToken '${project.version}', project.version
    replaceToken '${git.commit.id.abbrev}', 'git rev-parse --short HEAD'.execute().text.readLines()[0]
    replaceToken '${git.dirty}', 'git diff --quiet HEAD'.execute().waitFor() == 1
}

jar {
    manifest {
        attributes 'Main-Class': 'org.benf.cfr.reader.Main'
		attributes 'Implementation-Version': archiveVersion
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
    doFirst {
        def xml = new URL("https://maven.fabricmc.net/net/fabricmc/cfr/maven-metadata.xml").text
        def metadata = new XmlSlurper().parseText(xml)
        def versions = metadata.versioning.versions.version*.text();
        if (versions.contains(version)) {
            throw new RuntimeException("${version} has already been released!")
        }
    }
}

publish.mustRunAfter checkVersion
